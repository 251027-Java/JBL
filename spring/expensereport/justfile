maven := './mvnw'
debug_args := '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=*:5005'
classpath := '.gen-classpath'
java_sep := if os_family() == 'windows' { ';' } else { ':' }
db_container := 'expensereport-db'
package_name := 'com.revature.expensereport'
ddl_file := 'scripts/init.sql'
image_tag := 'minidomo/exprp-server'
docker_server := 'expenserp-server'
docker_network := 'exprp-network'

alias refresh := compile

# style check
[group('style')]
check:
    {{ maven }} spotless:check
    {{ maven }} checkstyle:check

# format all code
[group('style')]
format:
    {{ maven }} spotless:apply

# removes artifacts and temporary files
clean:
    {{ maven }} clean

# verify project integrity
verify:
    {{ maven }} verify

# cleans and verifies project integrity
[default]
default: clean verify

# launch the application
run: clean
    {{ maven }} spring-boot:run

# launch the application with the debugger
debug: clean
    {{ maven }} spring-boot:run -Dspring-boot.run.jvmArguments="{{ debug_args }}"

# compile the program
compile:
    {{ maven }} compile

# generate a DDL script from JPA entities
[group('db')]
ddl filepath=ddl_file:
    {{ maven }} test-compile
    {{ maven }} exec:java -Dexec.mainClass="scripts.GenerateDdl" -Dexec.classpathScope=test -Dexec.args="{{ package_name }} {{ filepath }}"

# builds the jar
build: clean
    {{ maven }} package -DskipTests

_db-create:
    docker run --name {{ db_container }} --network {{ docker_network }} -e POSTGRES_PASSWORD=secret -p 5432:5432 -d postgres

_db-init filepath:
    docker cp {{ filepath }} {{ db_container }}:/tmp/dbdata
    @sleep 2
    docker exec {{ db_container }} psql -U postgres -v ON_ERROR_STOP=1 -q -f tmp/dbdata

# create the container and set up the database
[group('db')]
db-create: _db-create (_db-init ddl_file)

# destroy the database container
[group('db')]
db-destroy:
    docker rm -f {{ db_container }}

# start the database container
[group('db')]
db-start:
    docker start {{ db_container }}

# stop the database container
[group('db')]
db-stop:
    docker stop {{ db_container }}

# export the database
[group('db')]
db-export filepath='expenserp.data':
    docker exec {{ db_container }} pg_dump -U postgres -F p postgres > {{ filepath }}

# create container with a given data file
[group('db')]
db-create-with filepath: _db-create (_db-init filepath)

build-image:
    docker build -t {{ image_tag }} -f ./Dockerfile .

docker-run:
    docker run --name {{ docker_server }} --network {{ docker_network }} --env-file .env -p 8080:8080 -d {{ image_tag }}

docker-destroy:
    docker rm -f {{ docker_server }}

docker:
    {{ maven }} spring-boot:run -Dspring-boot.run.profiles=docker

docker-network:
    docker network create {{ docker_network }}