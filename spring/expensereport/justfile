maven := './mvnw'
debug_args := '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=*:5005'
classpath := '.gen-classpath'
java_sep := if os_family() == 'windows' { ';' } else { ':' }

alias refresh := compile

# style check
[group('style')]
check:
    {{ maven }} spotless:check
    {{ maven }} checkstyle:check

# format all code
[group('style')]
format:
    {{ maven }} spotless:apply

# removes artifacts and temporary files
clean:
    {{ maven }} clean

# verify project integrity
verify:
    {{ maven }} verify

# cleans and verifies project integrity
[default]
default: clean verify

# launch the application
run: clean
    {{ maven }} spring-boot:run

# launch the application with the debugger
debug: clean
    {{ maven }} spring-boot:run -Dspring-boot.run.jvmArguments="{{ debug_args }}"

# compile the program
compile:
    {{ maven }} compile

_gen-classpath:
    {{ maven }} dependency:build-classpath -Dmdep.outputFile={{ classpath }}
    @echo "{{ java_sep }}target/classes" >> {{ classpath }}

# generate a DDL script from JPA entities
ddl filepath='scripts/init.sql': compile _gen-classpath
    rm -rf {{ filepath }}
    java -cp `cat {{ classpath }}` src/main/java/scripts/GenerateDdl.java {{ filepath }}
    docker run --rm -v /`pwd`:/work backplane/pgformatter -i {{ filepath }}

# builds the jar
build: clean
    {{ maven }} package spring-boot:repackage
